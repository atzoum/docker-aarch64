#!groovy

node {
    def imageName = 'aarch64-jenkins'

    stage('checkout') {
        git(url: 'https://github.com/atzoum/docker-aarch64.git', branch: "${env.ATZOUM_BRANCH ?: 'master'}")
        dir(imageName) {
            stash name: "overrides", includes: "README.md, Dockerfile"
            git(url: 'https://github.com/jenkinsci/docker.git', branch: "${env.JENKINS_BRANCH ?: 'master'}")
            unstash "overrides"
        }
    }
    
    withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'dockerhub', usernameVariable: 'DOCKER_USER', passwordVariable: 'DOCKER_PASS']]) {
        withEnv(["PATH+DOCKER=/var/jenkins_home/docker-1.12.5","DOCKER_HOST=tcp://${DOCKER_HOSTNAME}:2375"]) {
            
            stage('build') {
                
                echo "building ${imageName}"
                def JENKINS_SHA = sh(script: "curl https://repo.jenkins-ci.org/public/org/jenkins-ci/main/jenkins-war/${JENKINS_VERSION}/jenkins-war-${JENKINS_VERSION}.war.sha1", returnStdout: true).trim()
                sh """
                     docker build -t ${DOCKER_USER}/${imageName} --build-arg JENKINS_VERSION=${JENKINS_VERSION} --build-arg JENKINS_SHA=${JENKINS_SHA} ${imageName}/
                """
            }

            stage('push') {
                
                echo "pushing ${imageName}"
                sh """
                     docker login -u="$DOCKER_USER" -p="$DOCKER_PASS" && \\
                     docker push ${DOCKER_USER}/${imageName} && \\
                     docker tag ${DOCKER_USER}/${imageName} ${DOCKER_USER}/${imageName}:${JENKINS_VERSION} && \\
                     docker push ${DOCKER_USER}/${imageName}:${JENKINS_VERSION}
                """
            }

            stage('update description') {
                
                def fullDescription = readFile "${imageName}/README.md"
                def token = readJson(
                    sh(
                        returnStdout: true, 
                        script: "curl -s -H \"Content-Type: application/json\" -X POST -d '{\"username\": \"${DOCKER_USER}\", \"password\": \"${DOCKER_PASS}\"}\' https://hub.docker.com/v2/users/login/")
                    ).token

                httpRequest(
                    url: "https://hub.docker.com/v2/repositories/atzoum/${imageName}/", 
                    httpMode: 'PATCH', 
                    requestBody: writeJson([full_description: fullDescription]), 
                    customHeaders: [[name: 'Content-Type', value: 'application/json'], [name: 'Authorization', value: "JWT ${token}"]], 
                    validResponseCodes: '200', 
                    consoleLogResponseBody: true)
            }
        }
    }
}

@NonCPS
def readJson(String input) {
  final slurper = new groovy.json.JsonSlurper()
  return new HashMap<>(slurper.parseText(input))
}

@NonCPS
def writeJson(v) {
  groovy.json.JsonOutput.toJson(v)
}